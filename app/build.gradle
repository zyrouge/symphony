plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'io.github.zyrouge.symphony'
    compileSdk 33

    defaultConfig {
        applicationId "io.github.zyrouge.symphony"
        minSdk 28
        targetSdk 33

        versionCode 58
        versionName "2023.2.58"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            def storeFileEnv = System.getenv("SIGNING_KEYSTORE_FILE")
            storeFile storeFileEnv ? file(storeFileEnv) : null
            storePassword System.getenv("SIGNING_KEYSTORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def accompanist_version = '0.27.0'
    def navigation_version = '2.5.3'

    implementation "androidx.compose.material:material-icons-extended:$compose_material_version"
    implementation "androidx.navigation:navigation-compose:$navigation_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'io.coil-kt:coil-compose:2.2.2'

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material3:material3:$compose_material3_version"
    implementation "androidx.compose.material3:material3:$compose_material3_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}
